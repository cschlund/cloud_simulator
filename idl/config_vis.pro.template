;------------------------------------------------------------------------------
PRO CONFIG_VIS, settings, FILE=file, VARS=vars, REFS=refs, $
                          CCIOLD=cciold, PATTERN=pattern
;------------------------------------------------------------------------------

    ; subfolder of file location
    run = 'v5.7_fortran/'
    ; input
    inp = '/cmsaf/cmsaf-cld7/cschlund/output/simulator/' + run
    ; figure output
    fig = '/cmsaf/cmsaf-cld7/cschlund/figures/simulator/' + run
    ; simulator git repository
    rep = '/home/cschlund/Programme/idl/cloud_simulator/idl/subtools/'
    ; eps2png bash script
    eps = rep + 'eps2png.sh '
    ; latest CC4CL outputs
    IF ~KEYWORD_SET(cciold) THEN $
        cci = '/cmsaf/cmsaf-cld7/cschlund/input/vali_gui/' ELSE cci = ''

    ; create figure output path if not existing
    validres = VALID_DIR( fig )
    IF(validres EQ 0) THEN creatres = CREATE_DIR( fig )

    IF ~KEYWORD_SET(file) THEN BEGIN 
        ; get file list
        IF KEYWORD_SET(pattern) THEN BEGIN
            files = FINDFILE(inp+pattern, count=count)
        ENDIF ELSE BEGIN
            files = FINDFILE(inp+'*MM*nc', count=count)
        ENDELSE

        IF (count GT 0) THEN BEGIN 
            PRINT, '** ', STRTRIM(count,2), ' InputFiles found'
        ENDIF ELSE BEGIN 
            PRINT, '** NO FILES FOUND'
            RETURN
        ENDELSE
    ENDIF ELSE BEGIN
        files = [file]
        count = N_ELEMENTS(files)
    ENDELSE

    ; get variable list
    IF ~KEYWORD_SET(vars) THEN BEGIN 
        var_list = 'hist2d_cot_ctp'
        var_list = var_list + ',cfc,cph,cth,ctt,ctp'
        var_list = var_list + ',cwp,lwp,iwp'
        var_list = var_list + ',cwp_allsky,lwp_allsky,iwp_allsky'
        var_list = var_list + ',cer,cer_liq,cer_ice'
        var_list = var_list + ',cot,cot_liq,cot_ice'
        vars = STRSPLIT(var_list, /EXTRACT,',')
    ENDIF ELSE BEGIN
        vars = STRSPLIT(vars, /EXTRACT,',')
    ENDELSE

    ; set min, max values for plotting
    min_max = FLTARR(2,N_ELEMENTS(vars))
    FOR i=0, N_ELEMENTS(vars)-1 DO BEGIN 
        mini = 0. & maxi = 0.
        case vars[i] of 
            'cwp'           : begin & mini =   0. & maxi =  600. & end
            'cwp_allsky'    : begin & mini =   0. & maxi =  600. & end
            'lwp'           : begin & mini =   0. & maxi =  300. & end
            'lwp_allsky'    : begin & mini =   0. & maxi =  300. & end
            'iwp'           : begin & mini =   0. & maxi =  700. & end
            'iwp_allsky'    : begin & mini =   0. & maxi =  700. & end
            'cth'           : begin & mini =   0. & maxi =   15. & end
            'cot'           : begin & mini =   0. & maxi =   50. & end
            'cot_liq'       : begin & mini =   0. & maxi =   50. & end
            'cot_ice'       : begin & mini =   0. & maxi =   50. & end
            'cer'           : begin & mini =   0. & maxi =   50. & end
            'cer_liq'       : begin & mini =   0. & maxi =   50. & end
            'cer_ice'       : begin & mini =   0. & maxi =   50. & end
            'cfc'           : begin & mini =   0. & maxi =    1. & end
            'cph'           : begin & mini =   0. & maxi =    1. & end
            'ctt'           : begin & mini = 200. & maxi =  330. & end
            'ctp'           : begin & mini =  10. & maxi = 1000. & end
            'hist2d_cot_ctp': begin & mini =   1. & maxi =   10. & end
        else: 
    endcase
        min_max[0,i] = mini & min_max[1,i] = maxi
    ENDFOR


    ; possible reference data for inter-comparison
    reference_data = 'cci,gac2,myd,mod,myd2,mod2,pmx'
    ref_options = STRSPLIT(reference_data, /EXTRACT,',')

    ; get reference list
    IF KEYWORD_SET(refs) THEN refs = STRSPLIT(refs, /EXTRACT,',')


    settings = { inp_pwd:inp, out_pwd:fig, cci_pwd:cci, $
                 eps2png:eps, refdata:ref_options, $
                 files:files, nfiles:count, mini_maxi:min_max }

END
